MODULE QCI_LINEAR
   USE QCIPREC
   IMPLICIT NONE
   ! number of atoms for QCIlinear
   INTEGER :: NQCILINEAR = 0
   ! cutoff for QCIlinear treatment
   REAL(KIND=REAL64) :: LINEARCUT = 0.5D0
   ! list of linear atoms
   INTEGER, ALLOCATABLE :: LINEARATOMS(:)


   CONTAINS

      SUBROUTINE GET_LINEAR_ATOMS()
         USE QCIKEYS, ONLY: NATOMS
         USE MOD_INTCOORDS, ONLY: XSTART, XFINAL
         USE  HELPER_FNCTS, ONLY: DISTANCE_ATOM_DIFF_IMAGES
         INTEGER :: NDUMMY, DUMMYLIST(NATOMS)
         REAL(KIND=REAL64) :: DIST
         INTEGER :: J1

         NDUMMY = 0
         DUMMYLIST(1:NATOMS) = -1
         DO J1=1,NATOMS
            CALL DISTANCE_ATOM_DIFF_IMAGES(NATOMS, XSTART, XFINAL, J1, DIST)
            IF (DIST.LT.LINEARCUT) THEN
               NDUMMY = NDUMMY + 1
               DUMMYLIST(NDUMMY) = J1
            END IF
         END DO

         NQCILINEAR = NDUMMY
         CALL ALLOC_QCI_LINEAR()
         LINEARATOMS(1:NQCILINEAR) = DUMMYLIST(1:NQCILINEAR)
      END SUBROUTINE GET_LINEAR_ATOMS
   
      SUBROUTINE ALLOC_QCI_LINEAR()
         CALL DEALLOC_QCI_LINEAR
         ALLOCATE(LINEARATOMS(NQCILINEAR))
      END SUBROUTINE ALLOC_QCI_LINEAR

      SUBROUTINE DEALLOC_QCI_LINEAR()
         IF (ALLOCATED(LINEARATOMS)) DEALLOCATE(LINEARATOMS)
      END SUBROUTINE DEALLOC_QCI_LINEAR

END MODULE QCI_LINEAR